buildscript {
	dependencies {
		classpath 'org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1'
		classpath 'junit:junit:4.12'
		classpath "com.gradle.publish:plugin-publish-plugin:0.9.7"
	}
	repositories {
		maven { url "https://repository.jboss.org/nexus/content/groups/public/" }
		mavenLocal()
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
}

plugins {
    id 'java'
    id 'groovy'
    id 'maven-publish'
  	id "org.datlowe.maven-publish-auth" version "2.0.2"
  	id "com.gradle.plugin-publish" version "0.11.0"
    id 'signing'
}

defaultTasks 'clean', 'check', 'assemble'

group = 'org.jsweet'
version = '3.2.0-SNAPSHOT'
description = 'JSweet Gradle plugin'

sourceCompatibility = 11
targetCompatibility = 11
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

repositories {
	jcenter()
	mavenCentral()

	maven { url "https://repository.jsweet.org/artifactory/libs-release-local" }
	maven { url "https://repository.jsweet.org/artifactory/libs-snapshot-local" }
}

dependencies {

	implementation gradleApi()
	implementation localGroovy()

	implementation group: 'org.jsweet', name: 'jsweet-transpiler', version: project.version
	implementation group: 'org.codehaus.plexus', name: 'plexus-utils', version:'3.0.20'

	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

task sourceJar(type: Jar) { 
	from sourceSets.main.allJava 
}
task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

ext.mavenUser = System.getenv( "GRADLE_MAVEN_USER" )
ext.mavenPassword = System.getenv( "GRADLE_MAVEN_PASSWORD" )

publishing {
	repositories {
		maven {
			credentials {
        		username "$mavenUser"
        		password "$mavenPassword"
	        }
			name "jsweet-snapshots"
			url "http://repository.jsweet.org/artifactory/plugins-${project.version.endsWith('-SNAPSHOT') ? 'snapshot' : 'release' }-local"
		}
	}
	publications {
		maven(MavenPublication) {
			groupId = 'org.jsweet'
			artifactId = 'jsweet-gradle-plugin'


            pom {
                name = 'JSweet Gradle plugin'
                description = 'Gradle plugin for JSweet'
                url = 'http://www.jsweet.org'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'lgrignon'
                        name = 'Louis Grignon'
                        email = 'louis.grignon@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/cincheo/jsweet.git'
                    developerConnection = 'scm:git:git@github.com:cincheo/jsweet.git'
                    url = 'https://github.com/cincheo/jsweet'
                }
            }

			from components.java

			artifact sourceJar { classifier "sources" }
			artifact javadocJar { classifier "javadoc" }
		}
	}
}

signing {
    sign publishing.publications.maven
}

String skipSigning = System.getProperty("skipSigning")
tasks.withType(Sign)*.enabled = skipSigning == null || !skipSigning.equals("true")

pluginBundle {
  website = 'http://www.jsweet.org/'
  vcsUrl = 'https://github.com/cincheo/jsweet'
  description = 'JSweet Gradle plugin'
  tags = ['jsweet', 'transpiler', 'java', 'web', 'typescript', 'typescript', 'gradle']

  plugins {
    jsweetPlugin {
      id = 'org.jsweet.jsweet-gradle-plugin'
      displayName = 'JSweet Gradle plugin'
    }
  }
}

